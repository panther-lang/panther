using System
using System.IO

object Program {
    def main(): unit = {
        val args: Array<string> = Environment.GetCommandLineArgs()

        if(args.Length < 3) {
            printHelp()
        } else {
            printLogo()
            val trees = new SyntaxTree[args.Length - 2]
            for (x <- 2 to args.Length) {
                println("processing " + args[x] + "...")
                trees[x - 2] = MakeSyntaxTree.parse_file(args[x])
            }

            val compilation = MakeCompilation.create(trees)

            if (compilation.diagnostics.Length > 0) {
                for (i <- 0 to compilation.diagnostics.Length) {
                    printDiagnostic(compilation.diagnostics[i])
                }
            } else {
                println("emitting to " + args[1] + "...")
                compilation.emit(args[1])
            }
        }
    }

    def pad_right(value: string, len: int): string = {
        var padded = value
        while (padded.Length < len) {
            padded = padded + " "
        }

        padded
    }

    def printDiagnostic(diagnostic: Diagnostic): unit = {
        val location = diagnostic.location
        val span = location.span
        val source_file = location.source_file

        println(diagnostic.to_string())

        for (currrent_line <- location.start_line to location.end_line + 1) {
            val line = source_file.get_line(currrent_line)
            val start_in_current = source_file.get_line_index(span.start) == currrent_line
            val end_in_current = source_file.get_line_index(span.end) == currrent_line

            val prefix_end =
                if (start_in_current) span.start
                else line.start

            val suffix_start =
                if (end_in_current) span.end
                else line.end

            val prefix_span = TextSpanFactory.from_bounds(line.start, prefix_end)
            val error_span = TextSpanFactory.from_bounds(prefix_end, suffix_start)
            val suffix_span = TextSpanFactory.from_bounds(suffix_start, line.end)

            val prefix = source_file.to_string(prefix_span)
            val error = source_file.to_string(error_span)
            val suffix = source_file.to_string(suffix_span)

            print(prefix)
            print(ANSI.foreground_color("e06c75"))
            print(error)
            print(ANSI.Clear)
            println(suffix)

            for(c <- 0 to (prefix_span.length)) {
                print('-')
            }
            println('^')
        }

        println()
    }

    def printToken(token: SyntaxToken) =
        println(pad_right("TOKEN[" + SyntaxFacts.get_kind_name(token.kind) + "," + string(token.start) + "]: ", 36) + "\"" + token.text + "\"")

    def printLogo() = println("panther - 0.0.1")
    def printHelp() = println("pantherc output.c [sources]")
}